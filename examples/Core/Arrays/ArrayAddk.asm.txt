; ArrayAddk: add k to each element of an array
; Sigma16: https://jtod.github.io/home/Sigma16/  John O'Donnell, 2022

; Traverse an array and add constant k to each element

; An array x of n elements is defined below, as well as n (size of
; array x) and k (a constant).  The program traverses the array and
; adds k to each element

;   i := 0
;   while i < n do
;     x[i] := x[i] + k
;     i := i+1

; Register usage
;   R1 = i  loop counter
;   R2 = 1  constant for incrementing loop counter
;   R3 = n  size of array
;   R4 = k  constant to add to elements
;   R5      temporary

; Initialisation
      lea    R1,0[R0]     ; i := 0
      lea    R2,1[R0]     ; R2 := 1
      load   R3,n[R0]     ; R3 := n
      load   R4,k[R0]     ; R4 := k

loop  cmp    R1,R3        ; compare i with n
      jumpge done[R0]     ; if not (i < n) then goto done
      load   R5,x[R1]     ; R5 := x[i]
      add    R5,R5,R4     ; R5 := x[i] + k
      store  R5,x[R1]     ; x[i] := x[i] + k
      add    R1,R1,R2     ; i := i + 1
      jump   loop[R0]     ; goto loop

done  trap   R0,R0,R0     ; terminate the program

k     data    7   ; value to add to elements
n     data    5   ; size of array x
x     data   17   ; expected new value =  24 (0018)
      data   23   ; expected new value =  30 (001e)
      data  981   ; expected new value = 988 (03dc)
      data   -9   ; expected new value =  -2 (fffe)
      data   76   ; expected new value =  83 (0053)
